integer LISTEN_CHANNEL = 39;
integer LISTEN_HANDLE = 0;
integer CONTEO_VISITANTES = 0;
integer INICIA_STATE=0;
float TIMER_TICK = 5.0;
list NPC_NAMES = ["Profesor Maestro"];
list NOTECARDS = [];

key NPC = NULL_KEY;
vector POS_NPC = <0 , 0, 1.0>;
vector ROT_NPC = <0, 0, 210>;

integer NUMERO_HITOS = 4;
integer HITO_STEP = 2;
integer HITO_ACTUAL = 0;
string PREFIJO_HITO = "Escuela Hito ";
list HITOS = [];

string elegir_notecard(){
    integer indice = (integer) llFrand( llGetListLength( NOTECARDS ) );    
    return llList2String(NOTECARDS, indice);
}

secuencia_inicio(){
    NPC = osNpcCreate("Profesor", "Maestro", llGetPos() + POS_NPC, elegir_notecard(), 0 );
    osNpcSetRot(NPC, llEuler2Rot( ROT_NPC * DEG_TO_RAD ) );
    HITO_ACTUAL = 0;
    HITO_STEP = 1;
}

secuencia_bucle(){
    if( HITO_ACTUAL < 0 || HITO_ACTUAL >= NUMERO_HITOS){
        return;
    } 
    string objetivo = PREFIJO_HITO + (string) HITO_ACTUAL;
    //llOwnerSay("Buscando " + objetivo);
    vector posicion = llList2Vector(HITOS, HITO_ACTUAL);
    osNpcMoveToTarget(NPC, posicion + <0.0, 0.0, 1.0 >, OS_NPC_NO_FLY);
    llSleep(1.5);    
    osNpcSetRot(NPC, llEuler2Rot( ROT_NPC * DEG_TO_RAD ) );
        
    HITO_ACTUAL += HITO_STEP;
    if( HITO_ACTUAL >= NUMERO_HITOS ){
        HITO_STEP = -1;
        HITO_ACTUAL += HITO_STEP * 2;
        //HITO_ACTUAL = 0;
    }else if( HITO_ACTUAL < 0 ){        
        HITO_STEP = 1;
        HITO_ACTUAL = 0;
    }
    if( HITO_ACTUAL == 0 && HITO_STEP == 1){
        osNpcLoadAppearance( NPC, elegir_notecard() );
    }
}

secuencia_listen(integer channel, string name, key id, string message){
    
}

secuencia_fin(){
    osNpcRemove(NPC);
    NPC = NULL_KEY;
}

leer_listado_notecards(){
    integer i;    
    integer conteo = llGetInventoryNumber( INVENTORY_NOTECARD );
    NOTECARDS = [];
    for( i=0; i< conteo; i++){
        NOTECARDS += llGetInventoryName( INVENTORY_NOTECARD, i );
    }
}

default
{
    state_entry()
    {
        leer_listado_notecards();
        // Identifica los Hitos en el alrededor
        llSensor( "", NULL_KEY, PASSIVE | ACTIVE , 7.0, PI );
        CONTEO_VISITANTES = 0;
        INICIA_STATE = 0;
        LISTEN_HANDLE = llListen( LISTEN_CHANNEL, "", NULL_KEY, "");
        llOwnerSay("Acabo de ser inicializado");
    }
    state_exit(){
        llListenRemove(LISTEN_HANDLE);
    }
    
    listen(integer channel, string name, key id, string message){
        llOwnerSay("Recibo mensaje " + message);
        if(id == llGetOwner() && message == "estado"){
            llOwnerSay("Estado Actual: Default");
        }else if(message == "sentado"){
            CONTEO_VISITANTES ++;
            state visitado;
        }
    }
    
    collision_start(integer detected){        
        integer i;
        integer j;
        string nombre;
        integer encontrado;
                
        for( i=0; i < detected ; i++){
            nombre = llDetectedName(i);
            encontrado = FALSE;
            for(j=0 ; encontrado == FALSE && j< llGetListLength(NPC_NAMES); j++){
                if( nombre == llList2String(NPC_NAMES, j) ){
                    encontrado = TRUE;
                }
            }
            if( encontrado == FALSE ){
                //llSay(LISTEN_CHANNEL, "sentado");
                CONTEO_VISITANTES ++;
            }            
        }
        if(CONTEO_VISITANTES > 0){
            state visitado;
        }
    }
    sensor(integer detected){
        // Identifica los Hitos en el alrededor
        integer indice_detectado;
        integer indice_hito;
        integer hito_encontrado;        
        string nombre_detectado;
        string nombre_hito;
        vector posicion;
        HITOS = [];
        llOwnerSay("buscando hitos entre "+ detected + " objetos cercanos");
        for(indice_hito = 0; indice_hito < NUMERO_HITOS; indice_hito ++){
            nombre_hito = PREFIJO_HITO + (string)indice_hito;
            hito_encontrado = FALSE;
            for(indice_detectado = 0; hito_encontrado == FALSE && indice_detectado < detected; indice_detectado++){
                nombre_detectado = llDetectedName(indice_detectado);
                //llOwnerSay("sera " + nombre_detectado + "?");
                if(nombre_detectado == nombre_hito){
                    llOwnerSay("encontrado" + nombre_detectado);
                    posicion = llList2Vector( llGetObjectDetails( llDetectedKey( indice_detectado), [OBJECT_POS]), 0);
                    HITOS += posicion;
                    hito_encontrado = TRUE;
                }
            }            
        }
        llOwnerSay("Se ha cargado la posicion de "+ (string)llGetListLength(HITOS) + " hitos");        
    }    
}

state visitado{
    state_entry(){
        INICIA_STATE = 1;        
        secuencia_inicio();
        llSetTimerEvent(TIMER_TICK);
        llOwnerSay("Inicia Visita");
        LISTEN_HANDLE = llListen( LISTEN_CHANNEL, "", NULL_KEY, "");
    }
    listen(integer channel, string name, key id, string message){
        integer indice;
        if(id == llGetOwner() && message == "estado"){
            llOwnerSay("Estado Actual: Visitado");
        }else if(message == "sentado"){
            CONTEO_VISITANTES ++;
        }else if(message == "levantado"){
            CONTEO_VISITANTES --;           
        }else{
            secuencia_listen( channel,  name,  id, message);
        }
    }    
    state_exit(){
        llSetTimerEvent(0);
        llOwnerSay("Finaliza Visita");
        secuencia_fin();
        llListenRemove(LISTEN_HANDLE);
    }
    timer(){
        secuencia_bucle();
        if( CONTEO_VISITANTES <= 0){
            state default;
        }
    }
    
    collision_start(integer detected){
        integer i;
        integer j;
        string nombre;
        integer encontrado;
        for( i=0; i < detected ; i++){
            nombre = llDetectedName(i);
            encontrado = FALSE;
            for(j=0 ; encontrado == FALSE && j< llGetListLength(NPC_NAMES); j++){
                if( nombre == llList2String(NPC_NAMES, j) ){
                    encontrado = TRUE;
                }
            }
            if( encontrado == FALSE ){
                //llSay(LISTEN_CHANNEL, "sentado");
                CONTEO_VISITANTES ++;            
            }        
        }        
    }
    
    collision_end(integer detected){
        integer i;
        integer j;
        string nombre;
        integer encontrado;
        for( i=0; i < detected ; i++){
            nombre = llDetectedName(i);
            encontrado = FALSE;
            for(j=0 ; encontrado == FALSE && j< llGetListLength(NPC_NAMES); j++){
                if( nombre == llList2String(NPC_NAMES, j) ){
                    encontrado = TRUE;
                }
            }
            if( encontrado == FALSE ){
                //llSay(LISTEN_CHANNEL, "levantado");
                CONTEO_VISITANTES --;
            }            
        }
    }
} 